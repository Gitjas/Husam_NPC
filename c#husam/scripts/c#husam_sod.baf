/* bd0101.are: Husam will initate dialogue here. */

IF
	Global("C#HusamSpawn","GLOBAL",1)
	AreaCheck("bd0101")
	Global("C#Husam_InitiateDialog","bd0101",0)
	See(Player1)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_InitiateDialog","bd0101",1)
		StartDialogNoSet(Player1)
END


/* Move Husam to the table in the morning after the last night in the Ducal Palace (before heading out against the Crusade) */
IF
	Global("bd_plot","global",55)
	Global("C#Husam_bd_55","bd0103",0)
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	AreaCheck("bd0103")
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride("C#Husam1",JumpToPoint([479.286])) 
		ActionOverride("C#Husam1",Face(SW))
		SetGlobal("C#Husam_bd_55","bd0103",1)
END	


/* bd1000: Husam comments on the explosion on the bridge. This usually doesn't work */

IF
	GlobalTimerNotExpired("bd_caelar_timer","bd1000")
	AreaCheck("bd1000")
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Detect(Player1)	
	Global("C#Husam_SoDbridge","bd1000",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDbridge","bd1000",1)
		DisplayStringHead(Myself,@10021)
END


/* PC killed crusaders with the spikes */
IF
    OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
	AreaCheck("bd7230")
    Global("C#Husam_KillSpikes","bd7230",1)
    CombatCounter(0)
	!GlobalTimerNotExpired("C#SoD_spiketrap_timer","MYAREA")
THEN
	RESPONSE #100
		SetGlobal("C#Husam_KillSpikes","bd7230",2)
		SetGlobalTimer("C#SoD_spiketrap_timer","MYAREA",ONE_ROUND)
		DisplayStringHead(Myself,@10022)
END


/* BDTAKOS three heirlooms */
/* check for all 7th party member inventories. This might be a bit too thorough */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	AreaCheck("bd1000") //first camp
	Global("bd_122_accepted","global",1) 
	!PartyHasItem("bdmirr01")
	!HasItem("bdmirr01","C#Husam1")
	OR(3)
		!HasItem("bdmirr01","C#Solaufein")
		Global("C#SolauJoined","GLOBAL",0)
		!InParty("C#Solaufein")
	OR(3)
		!HasItem("bdmirr01","C#Grey")
		Global("C#GreyJoined","GLOBAL",0)
		!InParty("C#Grey")
	OR(3)
		!HasItem("bdmirr01","C#Brandock")
		Global("C#BrandockJoined","GLOBAL",0)
		!InParty("C#Brandock")
	Exists("BDTAKOS")
	!Dead("BDTAKOS")
	CombatCounter(0)
	Global("C#Husam_SoDHeirlooms1","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDHeirlooms1","LOCALS",1) 
END

IF

	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	AreaCheck("bd1000") //first camp
	Global("bd_122_accepted","global",1) 
	!PartyHasItem("bdclck04")
	!HasItem("bdclck04","C#Husam1")
	OR(3)
		!HasItem("bdclck04","C#Solaufein")
		Global("C#SolauJoined","GLOBAL",0)
		!InParty("C#Solaufein")
	OR(3)
		!HasItem("bdclck04","C#Grey")
		Global("C#GreyJoined","GLOBAL",0)
		!InParty("C#Grey")
	OR(3)
		!HasItem("bdclck04","C#Brandock")
		Global("C#BrandockJoined","GLOBAL",0)
		!InParty("C#Brandock")
	Exists("BDTAKOS")
	!Dead("BDTAKOS")
	CombatCounter(0)
	Global("C#Husam_SoDHeirlooms2","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDHeirlooms2","LOCALS",1) 
END

IF

	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	AreaCheck("bd1000") //first camp
	Global("bd_122_accepted","global",1) 
	!PartyHasItem("bdshld02")
	!HasItem("bdshld02","C#Husam1")
	OR(3)
		!HasItem("bdshld02","C#Solaufein")
		Global("C#SolauJoined","GLOBAL",0)
		!InParty("C#Solaufein")
	OR(3)
		!HasItem("bdshld02","C#Grey")
		Global("C#GreyJoined","GLOBAL",0)
		!InParty("C#Grey")
	OR(3)
		!HasItem("bdshld02","C#Brandock")
		Global("C#BrandockJoined","GLOBAL",0)
		!InParty("C#Brandock")
	Exists("BDTAKOS")
	!Dead("BDTAKOS")
	CombatCounter(0)
	Global("C#Husam_SoDHeirlooms3","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDHeirlooms3","LOCALS",1) 
END

/* if one of the heirlooms is not in party possession yet */
IF

	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	AreaCheck("bd1000") //first camp
	Global("bd_122_accepted","global",1) 
	OR(3)
		Global("C#Husam_SoDHeirlooms1","LOCALS",1)
		Global("C#Husam_SoDHeirlooms2","LOCALS",1)
		Global("C#Husam_SoDHeirlooms3","LOCALS",1)
	Exists("BDTAKOS")
	!Dead("BDTAKOS")
	CombatCounter(0)
	Global("C#Husam_SoDTakosHeirlooms","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDTakosHeirlooms","GLOBAL",1) 
END

IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Husam_SoDTakosHeirlooms","GLOBAL",1) 
	CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END


/* camp BD1000 petrified people */

IF

	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	AreaCheck("bd1000") //first camp
	OR(5)
		See("BDPETR01")
		See("BDPETR02")
		See("BDPETR03")
		See("BDPETR04")
		See("BDPETR05")
	TriggerOverride("bdpetr01",Range(Myself,30))
	TriggerOverride("bdpetr05",Range(Myself,30))
	StateCheck("BDPETR01",STATE_STONE_DEATH)
	StateCheck("BDPETR02",STATE_STONE_DEATH)
	StateCheck("BDPETR03",STATE_STONE_DEATH)
	StateCheck("BDPETR04",STATE_STONE_DEATH)
	StateCheck("BDPETR05",STATE_STONE_DEATH)
	CombatCounter(0)
	OR(3) //should talk after Brandock
		GlobalGT("C#Brandock_SoDStonePeople","GLOBAL",1)
		Global("C#BrandockJoined","GLOBAL",0)
		StateCheck("C#Brandock",CD_STATE_NOTVALID)
	Global("C#Husam_SoDStonePeople","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDStonePeople","GLOBAL",1) 
END

IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Husam_SoDStonePeople","GLOBAL",1) 
	CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END


/* main coalition camp - thieves' guild extention */

/* Being near Waizahb will trigger husam's dialogue.
1. no quest from Stonehand yet: Global("bd_sdd305_den_of_thieves","global",0)
2. quest received: Global("bd_sdd305_den_of_thieves","global",1)

Global("C#HU_
*/

/* near Waizahb - activation for the different cases */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
//	GlobalLT("bd_plot","global",##) //before main camp attack
	AreaCheck("bd3000")
	!Dead("bdwaizah")
	See("bdwaizah")
	OR(2)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",0)
		Global("bd_sdd305_den_of_thieves","global",1)
	OR(3)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",0)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",2)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",4)
THEN
	RESPONSE #100
		IncrementGlobal("C#Husam_SoDCampThievesGuild","GLOBAL",1)
END

/* initiate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  OR(3)
	Global("C#Husam_SoDCampThievesGuild","GLOBAL",1)
	Global("C#Husam_SoDCampThievesGuild","GLOBAL",3)
	Global("C#Husam_SoDCampThievesGuild","GLOBAL",5)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#HusamDialogTimer","GLOBAL",2400)
    StartDialogNoSet(Player1)
END

/* PC told Husam to deal with the Thieves Guild without talking to Waizahb: tell PC it's done */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
//	GlobalLT("bd_plot","global",##) //before main camp attack
	GlobalTimerExpired("C#HU_SoDCampTGTimer","GLOBAL")
	AreaCheck("bd3000")
	!Dead("bdwaizah")
	See("bdwaizah")
	Global("C#Husam_SoDCampThievesGuild","GLOBAL",7)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDCampThievesGuild","GLOBAL",10)
END

/* initiate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  Global("C#Husam_SoDCampThievesGuild","GLOBAL",10)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#HusamDialogTimer","GLOBAL",2400)
    StartDialogNoSet(Player1)
END


/* Husam reacts to Thrix's game */

/* PC offered Husam's soul */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#Husam_SoDThrix","GLOBAL",2)
		Global("C#Husam_SoDThrix","GLOBAL",3)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		IncrementGlobal("C#Husam_SoDThrix","GLOBAL",3)
END

/* PC offered someone's soul */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!Global("C#Husam_SoDThrix","GLOBAL",2)
	!Global("C#Husam_SoDThrix","GLOBAL",3)
	GlobalLT("C#Husam_SoDThrix","GLOBAL",7)
	Global("bd_thrix_sacrifice_companion","global",1)
	!Global("bd_thrix_sacrifice_self","global",1)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDThrix","GLOBAL",7)
END

IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(3)
		Global("C#Husam_SoDThrix","GLOBAL",7)
		Global("C#Husam_SoDThrix","GLOBAL",6)
		Global("C#Husam_SoDThrix","GLOBAL",5)		
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* back from Avernus */

/* activation */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("bd_plot","global",590)
	See("BDDAZZO")
	AreaCheck("bd4300")
	Global("C#Husam_SoDEnd","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDEnd","GLOBAL",1)
END

/* trigger */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Husam_SoDEnd","GLOBAL",1)	
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END


/* talk to PC at sewer exit */
IF
	AreaCheck("bd6000")
	Global("C#HusamJoined","GLOBAL",0)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Husam_SoDEnd","GLOBAL",4)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END


/* item reactions */

/* bddagg03 "Gemblade +1" */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	CombatCounter(0)
	PartyHasItemIdentified("bddagg03") 
	Global("C#Husam_SoDbddagg03","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDbddagg03","GLOBAL",1)
END
/* initiate */
IF 
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  Global("C#Husam_SoDbddagg03","GLOBAL",1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* Gold Bust bdmisc57 */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	CombatCounter(0)
	PartyHasItemIdentified("bdmisc57") 
	Global("C#Husam_SoDbdmisc57","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDbdmisc57","GLOBAL",1)
END
/* initiate */
IF 
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  Global("C#Husam_SoDbdmisc57","GLOBAL",1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* more dialogues that use the friendship dialogue timer */

/* Husam sold the gold bust */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !AreaType(DUNGEON)
  Global("C#Husam_SoDbdmisc57","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDbdmisc57","GLOBAL",4)
END
/* initiate */
IF 
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  Global("C#Husam_SoDbdmisc57","GLOBAL",4) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



/* Follow up: pondering about the camp Thieves Guild  */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
//	GlobalLT("bd_plot","global",##) //before main camp attack
//	GlobalTimerExpired("C#HU_SoDCampTGTimer","GLOBAL")
	AreaCheck("bd3000")
	!Dead("bdwaizah")
	See("bdwaizah")
	OR(6)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",12)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",14)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",17)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",6)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",11)
		Global("C#Husam_SoDCampThievesGuild","GLOBAL",18)
	Global("C#Husam_SoDCampTGFollowUp","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_SoDCampTGFollowUp","LOCALS",1)
END

/* initiate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  Global("C#Husam_SoDCampTGFollowUp","LOCALS",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#HusamDialogTimer","GLOBAL",2400)
    StartDialogNoSet(Player1)
END


/* friendship dialogues, timered only */

/* 3nd FD, after meeting Caelar at the bridge */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !AreaType(DUNGEON)
	GlobalGT("bd_plot","global",170)
	Global("C#Husam_Dialog","GLOBAL",5)
THEN
	RESPONSE #100
		IncrementGlobal("C#Husam_Dialog","GLOBAL",1)
END

/* 4th FD, sometime after the Boareskyr Bridge incident */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !AreaType(DUNGEON)
	GlobalGT("chapter","global",9) //after it happened
	GlobalGT("C#Husam_SoDBoareskyrBridgeScene","GLOBAL",0) //first bridge dialogue run
	Global("C#Husam_Dialog","GLOBAL",7)
THEN
	RESPONSE #100
		IncrementGlobal("C#Husam_Dialog","GLOBAL",1)
END


/* general timers */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  !AreaType(DUNGEON)
  OR(2)
	Global("C#Husam_Dialog","GLOBAL",1)
	Global("C#Husam_Dialog","GLOBAL",3) 
//    Global("C#Husam_Dialog","GLOBAL",5) set above
//    Global("C#Husam_Dialog","GLOBAL",7) set above
THEN
  RESPONSE #100
    IncrementGlobal("C#Husam_Dialog","GLOBAL",1)
END

/* FP dialogue triggering: SoD */
IF
%IT_IS_SOD%
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  OR(4)
    Global("C#Husam_Dialog","GLOBAL",2)  // about Sarevok
    Global("C#Husam_Dialog","GLOBAL",4)  // weak poison in palace attack
    Global("C#Husam_Dialog","GLOBAL",6)  // about Caelar after meeting her at the bridge
    Global("C#Husam_Dialog","GLOBAL",8)  // how do people see you?
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#HusamDialogTimer","GLOBAL",2400)
    StartDialogNoSet(Player1)
END
