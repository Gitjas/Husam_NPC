/* C#Husam_base: Husam's main script. Valid for all games */

/* in case Husam follows as 7th party member and Player1 was rested via script and not via  RestParty() */
/* spell and principle taken from Zed Nocear's BG1 Trigger Simulations! */

IF
	Global("C#HusamRestCounter","GLOBAL",1)
	RealGlobalTimerExpired("C#7thPartyMemberRestAfterTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("C#HusamRestCounter","GLOBAL",0)
END

IF
	Global("C#HusamRestCounter","GLOBAL",0)
	!RealGlobalTimerExpired("C#7thPartyMemberRestAfterTimer","GLOBAL")
	Global("C#HusamJoined","GLOBAL",2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#HusamRestCounter","GLOBAL",1)
		Rest()
		ReallyForceSpellRES("c#7rest",Myself)		
		SetInterrupt(TRUE)
END


/* follows as familiar: reset variable for refreshing of min HP spell after fight */

IF
	Global("C#Husam_Has1HPEffect","GLOBAL",2)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("C#Husam_Has1HPEffect","GLOBAL",1)
END

/* refresh min HP spell before a fight */
IF
	Global("C#Husam_Has1HPEffect","GLOBAL",1)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	See([ENEMY])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Husam_Has1HPEffect","GLOBAL",2)
		ReallyForceSpellRES("C#MIN5HP","C#Husam1") 
		SetInterrupt(TRUE)
END



/* follows as familiar: fight is over - Husam had passed out at 1 HP. Regains consciousness and gets healing boost to be ready to be taken into the group and not die upon joining the group */

IF
	Global("C#Husam_IsUnconscious","GLOBAL",1)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		ReallyForceSpellRES("c#7rest","C#Husam1") 	
		SetGlobal("C#Husam_IsUnconscious","GLOBAL",0)		
		DisplayStringHead(Myself,@10018)
		ChangeEnemyAlly(Myself, FAMILIAR)
		AddFamiliar()
		SetDialog("C#Husamj")
		//ChangeAIScript("DEFAULT",CLASS)
		ChangeAIScript("THIEF4",CLASS)
		ChangeAIScript("",RACE)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",DEFAULT)
		SetGlobal("C#LevelUp","LOCALS",0)
		SetInterrupt(TRUE)
END

/* follows as familiar: fight ends - resetting health warn variable */
IF
	Global("C#Husam_HPAlarmDone","GLOBAL",1)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_HPAlarmDone","GLOBAL",0)
END

/* follows as familiar: Husam warns about HP too low: auto pause. This can be switched on and off via PID */

IF
	Global("C#HPAlarm","LOCALS",1)  //PC told Husam to warn
	Global("C#Husam_HPAlarmDone","GLOBAL",0)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	HPPercentLT(Myself,45)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@10019) 		
		SetGlobal("C#Husam_HPAlarmDone","GLOBAL",1)
		PauseGame()
END

/* follows as familiar: Husam warns about HP too low: no auto pause. This can be switched on and off via PID */

IF
	Global("C#HPAlarm","LOCALS",0)  //PC told Husam not to warn
	Global("C#Husam_HPAlarmDone","GLOBAL",0)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	HPPercentLT(Myself,45)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@10019) 		
		SetGlobal("C#Husam_HPAlarmDone","GLOBAL",1)
END



/* follows as familiar: Husam passes out if at 1 HP and following as a 7th party member (otherwise he would be a killing machine) */

IF
	Global("C#Husam_IsUnconscious","GLOBAL",0)
	Global("C#HusamJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	Range(NearestEnemyOf(Myself),10)
	HPLT(Myself,6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,@10020) 
		ClearActions(Myself)
		RemoveFamiliar()
		ChangeEnemyAlly(Myself,NEUTRAL)
		ChangeAIScript("",CLASS)
		ChangeAIScript("",RACE)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",DEFAULT)	
		SetGlobal("C#Husam_IsUnconscious","GLOBAL",1)
		SetInterrupt(TRUE)
END

/* follows as familiar: Husam is at 1 HP and passes out */
IF
	Global("C#Husam_IsUnconscious","GLOBAL",1)
	!CombatCounter(0)
THEN
	RESPONSE #100
		PlayDead(100)
END


/* following as familiar/7th party member. Give 1 HP effect */

IF
	!InParty(Myself)
	Global("C#HusamJoined","GLOBAL",2)
	Global("C#Husam_Has1HPEffect","GLOBAL",0)
THEN
  RESPONSE #100
		SetInterrupt(FALSE)
		ReallyForceSpellRES("c#7rest","C#Husam1") //make sure there is no negative effect
		ReallyForceSpellRES("C#MIN5HP","C#Husam1") //give him min 5 HP and immunities
		SetGlobal("C#Husam_Has1HPEffect","GLOBAL",1)
		IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",1) 
		SetInterrupt(TRUE)	
END


/* Husam joined the party as full member. remove 1 HP effect */

IF
	InParty(Myself)
	Global("C#Husam_Has1HPEffect","GLOBAL",1)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	ReallyForceSpellRES("C#MIN0HP","C#Husam1")
	SetGlobal("C#Husam_Has1HPEffect","GLOBAL",0)
	IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",-1)
	SetInterrupt(TRUE)	
END


/* talk to PC if kicked out unhappy */
IF
	!InParty(Myself)
	Global("C#HusamJoined","GLOBAL",1)
	!HappinessGT(Myself,-299)
THEN
	RESPONSE #100
		Dialogue(Player1)
END


/* reputation comment - once in whole game/trilogy */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	RealGlobalTimerExpired("C#HusamDialogTimer","GLOBAL")
	OR(2)
		GlobalGT("C#Husam_Dialog","GLOBAL",4)
		GlobalGT("C#Husam_Dialog","GLOBAL",104) //## BGII
	OR(2)
		GlobalLT("C#Husam_Dialog","GLOBAL",99) //noch SoD
		GlobalGT("C#Husam_Dialog","GLOBAL",104) //## BGII
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		 ReputationLT(Player1,5)
		 ReputationGT(Player1,16)
	Global("C#Husam_RepComment","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Husam_RepComment","GLOBAL",1)
END

/* trigger */
IF
	OR(2) InParty(Myself) Global("C#HusamJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#Husam_RepComment","GLOBAL",1)	
THEN
	RESPONSE #100 
		RealSetGlobalTimer("C#HusamDialogTimer","GLOBAL",900)
		StartDialogNoSet(Player1)
END



/* start banter script if not in party */
IF
	%IT_IS_SOD%
	Global("C#HusamJoined","GLOBAL",2)
	RealGlobalTimerExpired("C#HusamNPCBanterTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
	ActionListEmpty()
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#HusamNPCBanterTimer","GLOBAL",1800)
		Interact(Player1)
END


IF
	Global("C#HusamJoined","GLOBAL",2)
	RealGlobalTimerExpired("C#HusamNPCBanterTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
	ActionListEmpty()
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#HusamNPCBanterTimer","GLOBAL",3000)
		Interact(Player1)
END


